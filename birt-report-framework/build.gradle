plugins {
  id 'base'
  id 'maven-publish'
  id 'signing'
}

group 'com.github.openequella'
version '4.16.0'

final frameworkFolder = file("./framework")
final osgiJarFolder = file("../birt-osgi/osgi/ReportEngine/platform/plugins")

final originalFile = file("framework.zip")

final artifactName = "${project.name}-${project.version}.zip"

class OSGIJar {
  File path
  String name

  OSGIJar(File path, String name) {
        this.path = path
        this.name = name
    }
}

task prepareBirtReportFramework(description: 'Prepare BIRT Report Framework') {
  doFirst {
    println("Download the original BIRT Report Framework package")
    new URI("https://www.eclipse.org/downloads/download.php?file=/birt/updates/release/4.16.0/downloads/birt-report-framework-sdk-4.16.0-202406141054.zip&r=1").toURL().withInputStream {      originalFile << it
    }

    println("Unzip the BIRT Report Framework package")
    copy {
      from zipTree(originalFile).matching {
        include 'plugins/*.jar'
        eachFile { f ->
          f.path = f.path.replaceFirst("plugins", '')
        }
        includeEmptyDirs = false
      }
      into frameworkFolder
    }

    println("Copy required Jars from the BIRT OSGI package")
    final jars = [
      'org.apache.batik.i18n_1.17.0.v20231215-1130.jar',
      'org.eclipse.birt.report.engine.emitter.config.excel_4.16.0.v202406141054.jar',
      'org.eclipse.birt.report.engine.emitter.prototype.excel_4.16.0.v202406141054.jar',
      'org.eclipse.core.runtime_3.31.100.v20240524-2010.jar',
      'org.eclipse.datatools.connectivity_1.15.0.202311071249.jar',
      'org.eclipse.datatools.connectivity.oda_3.7.0.202311071249.jar',
      'org.eclipse.datatools.connectivity.oda.consumer_3.5.0.202311071249.jar',
      'org.eclipse.emf.common_2.30.0.v20240314-0928.jar',
      'org.eclipse.emf.ecore_2.36.0.v20240203-0859.jar',
      'org.eclipse.emf.ecore.xmi_2.37.0.v20231208-1346.jar',
      'org.eclipse.emf.ecore.change_2.16.0.v20231208-1346.jar',
      'org.eclipse.equinox.common_3.19.100.v20240524-2011.jar',
      'org.eclipse.equinox.preferences_3.11.100.v20240327-0645.jar',
      'org.eclipse.equinox.registry_3.12.100.v20240524-2011.jar',
      'org.eclipse.osgi_3.20.0.v20240509-1421.jar',
    ].collect { name -> new OSGIJar(osgiJarFolder, name) }

    final nestedJars = [
      new OSGIJar(file("${osgiJarFolder.absolutePath}/org.eclipse.birt.report.data.oda.jdbc_4.16.0.v202406141054"), 'oda-jdbc.jar')
    ]

    (jars + nestedJars)
      .each {
      jar ->
        copy {
          from jar.path
          include jar.name
          into frameworkFolder
        }
    }

    println("Extract Tidy.jar from one of the Report Engine Jars and copy to here")
    copy {
      from zipTree(file("${osgiJarFolder}/org.eclipse.birt.report.engine_4.16.0.v202406141054.jar")).matching {
        include 'lib/Tidy.jar'
        eachFile { f ->
          f.path = f.path.replaceFirst("lib", '') // Only copy the Jar, not the folder.
        }
        includeEmptyDirs = false
      }
      into file(frameworkFolder)
    }

    println("Delete Jar 'javax.xml' which is not compatible with oEQ")
    file("${frameworkFolder}/javax.xml_1.3.4.v201005080400.jar").delete()
  }
}

task buildBirtReportFramework(description: 'Create the openEquella specific BIRT Report Framework', type: Zip) {
  archiveFileName = artifactName
  from fileTree(frameworkFolder)
  destinationDirectory = file("./")

  doLast {
    println("BIRT Report Framework has been successfully created.")
  }
}

buildBirtReportFramework.dependsOn(prepareBirtReportFramework)

publishing {
  publications {
    birtReportFramework(MavenPublication) {
      artifact artifactName
      pom {
        name = 'BIRT Report Framework'
        packaging = 'zip'
        description = 'BIRT Report Framework for openEquella'
        url = 'https://github.com/openequella/birt-packages'
        licenses {
          license {
            name = 'The Apache License, Version 2.0'
            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }
        scm {
          connection = 'scm:git:https://github.com/openequella/birt-packages.git'
          developerConnection = 'scm:git:https://github.com/openequella/birt-packages.git'
          url = 'https://github.com/openequella/birt-packages'
        }
        developers {
          developer {
            id = developerId
            name = developerName
            email = developerEmail
          }
        }
      }
    }
  }
  repositories {
    maven {
      url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
      credentials {
        username = ossrhUsername
        password = ossrhPassword
      }
    }
  }
}

signing {
  sign publishing.publications.birtReportFramework
}

clean {
  doLast {
    frameworkFolder.deleteDir()
    originalFile.delete()
    file(artifactName).delete()
  }
}
