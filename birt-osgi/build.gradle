plugins {
  id 'base'
  id 'maven-publish'
  id 'signing'
}

group 'com.github.openequella'
version '4.16.0'

final osgiFolder = file("./osgi")
final reportEngineFolder = file("${osgiFolder}/ReportEngine")
final platformFolder = file("${reportEngineFolder}/platform")

final originalFile = file("osgi.zip")
final compatibilityJar = file("${platformFolder}/plugins/org.eclipse.osgi.compatibility.state-1.2.500.jar")

final artifactName = "${project.name}-${project.version}.zip"

task prepareBirtOsgi(description: 'Prepare BIRT OSGI') {
  doFirst {
    println("Download the original BIRT OSGI package")
    new URI("https://www.eclipse.org/downloads/download.php?file=/birt/updates/release/4.16.0/downloads/birt-runtime-osgi-4.16.0-202406141054.zip&r=1").toURL().withInputStream {
      originalFile << it
    }

    println("Unzip the BIRT OSGI package")
    copy {
      from zipTree(originalFile)
      into osgiFolder
    }
  }
}

task buildBirtOsgi(description: 'Create the openEquella specific BIRT OSGI library', type: Zip) {
  archiveFileName = artifactName
  from fileTree(reportEngineFolder)
  exclude '/samples'
  exclude '/genReport.*'
  destinationDirectory = file("./")

  doLast {
    println("BIRT OSGI library has been successfully created.")
  }
}

buildBirtOsgi.dependsOn(prepareBirtOsgi)

publishing {
  publications {
    birtOsgi(MavenPublication) {
      artifact artifactName
      pom {
        name = 'BIRT OSGI runtime library'
        packaging = 'zip'
        description = 'BIRT OSGI runtime library for openEquella'
        url = 'https://github.com/openequella/birt-packages'
        licenses {
          license {
            name = 'The Apache License, Version 2.0'
            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }
        scm {
          connection = 'scm:git:https://github.com/openequella/birt-packages.git'
          developerConnection = 'scm:git:https://github.com/openequella/birt-packages.git'
          url = 'https://github.com/openequella/birt-packages'
        }
        developers {
          developer {
            id = developerId
            name = developerName
            email = developerEmail
          }
        }
      }
    }
  }
  repositories {
    maven {
      url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
      credentials {
        username = ossrhUsername
        password = ossrhPassword
      }
    }
  }
}

signing {
  sign publishing.publications.birtOsgi
}

clean {
  doLast{
    osgiFolder.deleteDir()
    originalFile.delete()
    compatibilityJar.delete()
    file(artifactName).delete()
  }
}
